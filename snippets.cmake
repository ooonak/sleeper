macro(enable_clang_tidy)
  find_program(clang_tidy_cmd NAMES "clang-tidy")
  if(NOT clang_tidy_cmd)
    message(WARNING "Could not find clang-tidy!")
  else()
    if(NOT EXISTS "${CMAKE_SOURCE_DIR}/.clang-tidy")
      message(FATAL_ERROR "Missing config file ${CMAKE_SOURCE_DIR}/.clang-tidy!")
    endif()
    set(CMAKE_CXX_CLANG_TIDY "${clang_tidy_cmd}")
  endif()
endmacro()

macro(enable_cppcheck)
  find_program(clang_cppcheck_cmd NAMES "cppcheck")
  if(NOT clang_cppcheck_cmd)
    message(WARNING "Could not find cppcheck!")
  else()
    set(CMAKE_CXX_CPPCHECK "${clang_cppcheck_cmd}")
  endif()
endmacro()

macro(enable_iwyu)
  find_program(iwyu_cmd NAMES "include-what-you-use")
  if(NOT iwyu_cmd)
    message(WARNING "Could not find include-what-you-use!")
  else()
    set(CMAKE_CXX_INCLUDE_WHAT_YOU_USE "${iwyu_cmd}")
  endif()
endmacro()

